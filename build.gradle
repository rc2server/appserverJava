apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'edu.wvu.stat'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

task fatJar(type:Jar) {
	manifest.from jar.manifest
	classifier = 'all'
	baseName = project.name + '-all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it)  } }
	with jar
}

jar {
	manifest {
		attributes ('Main-Class': 'edu.wvu.stat.rc2.Rc2Application', 
		'Class-Path': '.')
	}
}

artifacts {
	archives fatJar
}

test {
	testLogging.showStandardStreams = true
	systemProperties 'logback.configurationFile' : new File(projectDir,'src/test/resources/logback-test.xml').absolutePath
}

tasks.withType(Test) {
	systemProperties 'logback.configurationFile' : 'logback-test.xml'
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

repositories {
    mavenLocal()
     maven { url "https://jitpack.io" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
	logging.captureStandardOutput LogLevel.INFO
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.6.1'
    compile group: 'com.github.jeremyh', name: 'jBcrypt', version:'jbcrypt-0.4'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version:'9.2.13.v20150730'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version:'9.2.13.v20150730'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version:'0.9.0-rc4'
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version:'0.9.0-rc4'
    compile group: 'io.dropwizard', name: 'dropwizard-forms', version:'0.9.0-rc4'
    compile group: 'io.dropwizard', name: 'dropwizard-testing', version:'0.9.0-rc4'
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version:'0.9.0-rc4'
    compile group: 'com.impossibl.pgjdbc-ng', name: 'pgjdbc-ng', version:'0.7-SNAPSHOT'
    compile group: 'com.sun.mail', name: 'javax.mail', version:'1.5.4'
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version:'1.7.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version:'1.3'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version:'1.2.3'
    testCompile group: 'org.glassfish.jersey.test-framework', name: 'jersey-test-framework-core', version:'2.22'
    compile(group: 'com.google.auto.value', name: 'auto-value', version:'1.1') {
       /* This dependency was originally in the Maven provided scope, but the project was not of type war.
       This behavior is not yet supported by Gradle, so this dependency has been converted to a compile dependency.
       Please review and delete this closure when resolved. */
    }
}
